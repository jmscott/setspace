#
#  Synopsis:
#	Populate a PostgreSQL schema with the facts extracted by Apache pdfbox2 
#  See:
#	schema.sql
#

boot
{
	brr_capacity = 16;
	flow_worker_count = 8;
	os_exec_worker_count = 4;
	os_exec_capacity = 4;

	xdr_roll_duration = "24h";
	fdr_roll_duration = "24h";
	qdr_roll_duration = "24h";

	heartbeat_duration = "10s";
}

sql database setspace
{
	driver_name = "postgres";
	data_source_name = "sslmode=disable";
	max_idle_conns = 1;
	max_open_conns = 8;
}

tail brr
{
	path = "spool/prefixio.brr";
}

command brr_on_network
{
	path = "true";
}

call brr_on_network()
  when
  (
        (
		brr.verb == "put"
		or
		brr.verb == "get"
		or
		brr.verb == "eat"
		or
		brr.verb == "wrap"
		or
		brr.verb == "roll"
	)
	and
	brr.chat_history == "ok"
  ) or (
	brr.verb == "give"
	and
	brr.chat_history == "ok,ok"
  )
;

sql query in_pddocument_table row
{
	statement = `

	SELECT
		EXISTS (
		  SELECT
		  	blob
		    FROM
		    	pdfbox2.pddocument
		    WHERE
		    	blob = $1::udig
		),
		(
		  SELECT
		  	exit_status = 0
		    FROM
		    	pdfbox2.pddocument
		    WHERE
		    	blob = $1::udig
		)
	;
	`;

	result row is (
		answer bool,
		is_pdf bool
	);

	database is setspace;
}

query in_pddocument_table(brr.udig)
  when
  	brr_on_network.exit_status == 0
;

command json_pddocument
{
	path = "sbin/json-pddocument";
	exit_status is OK when in {0, 1};
}

call json_pddocument(brr.udig)
  when
  	in_pddocument_table.answer == false
;

sql query in_extract_utf8_table row
{
	statement = `

	SELECT
		EXISTS (
		  SELECT
		  	blob
		    FROM
		    	pdfbox2.extract_utf8
		    WHERE
		    	blob = $1::udig
		)
	;
	`;

	result row is (
		answer bool
	);

	database is setspace;
}

query in_extract_utf8_table(brr.udig)
  when
	#  is a pdf file in table pddocument
  	in_pddocument_table.is_pdf == true

	or

	#  or just created json pddocument
	json_pddocument.exit_status == 0
;

command json_extract_utf8
{
	path = "sbin/json-extract_utf8";
}

call json_extract_utf8(brr.udig)
  when
  	in_extract_utf8_table.answer == false
;
