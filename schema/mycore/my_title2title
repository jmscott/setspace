#!/usr/bin/env perl
#
#  Synopsis:
#	Convert my_title rows into json requests for mycore.title.
#  Usage:
#	psql -f select-my_title.sql | head -1 | my_title2title
#  See Also:
#	select-my_title.sql
#

use utf8;

#  Code lifted from JSON-Tiny
my %ESCAPE = (
	'"'     => '"',
	'\\'    => '\\',
	'b'     => "\x08",
	'f'     => "\x0c",
	'n'     => "\x0a",
	'r'     => "\x0d",
	't'     => "\x09",
	'u2028' => "\x{2028}",		#  unicode line-separator
	'u2029' => "\x{2029}"		#  unicode paragraph-separator
);

my %REVERSE = map { $ESCAPE{$_} => "\\$_" } keys %ESCAPE;

for(0x00 .. 0x1f) {
	my $packed = pack 'C', $_;
	$REVERSE{$packed} = sprintf '\u%.4X', $_
			unless defined $REVERSE{$packed};
}

sub utf82json_string
{
	my $str = shift;
	$str =~ s!([\x00-\x1f\x{2028}\x{2029}\\"])!$REVERSE{$1}!gs;
	return "\"$str\"";
}

die 'wrong number of arguments' unless @ARGV == 0;

sub read_tuple
{
	chomp;
	return split("\t");
}

while (<STDIN>) {
	my ($blob, $title, $blob_discover_unix_epoch) = read_tuple;

	die "field #1: blob: not a udig: $blob"
		unless $blob =~ m/^[a-z][a-z0-9]{0,7}:[[:graph:]]{32,128}$/;
	die "field #2: title: longer than 511 chars: $title"
					unless length($title) < 512; 

	#  Note: not fully match time: 2012-10-18 16:59:21.747884-05
	die "field #3: blob_discover_unix_epoch: not digits: " .
	    "$blob_discover_unix_epoch"
				unless $blob_discover_unix_epoch =~ m/^\d{10}$/;
	$blob = utf82json_string($blob);

	my $new_title = $title;

	#  control characters replaced with space
	$new_title =~ s/[[:cntrl:]]+/ /g;
	
	#  strip leading an trailing space
	$new_title =~ s/^[[:space:]]+|[[:space:]]+$//g;

	#  compress white space
	$new_title =~ s/[[:space:]]{2,}//g;

	$title = utf82json_string($title);
	$new_title = utf82json_string($new_title);
	my $request_unix_epoch = time();
	print <<END;
{
	"mycore.schema.setspace.com": {
		"title": {
			"blob": $blob,
			"title": $new_title
		}
	},
	"version1.schema.setspace.com": {
		"my_title.public": {
			"blob": $blob,
			"title": $title,
			"blob-discover-unix-epoch": $blob_discover_unix_epoch
		}
	},
	"note": "batch loaded by jmscott with script sbin/my_title2title",
	"request-unix-epoch": $request_unix_epoch
}
END
}
